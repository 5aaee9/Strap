plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

group 'me.indexyz.strap'
version '0.0.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

compileJava.options.encoding = 'UTF-8'

sourceSets {
    main {
        java {
            srcDirs = ['src/java']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }
}

repositories {
    mavenCentral()
}

def deps = [
    'org.json:json:20180813',
    'com.google.guava:guava:26.0-jre',
    'com.squareup.okhttp3:okhttp:3.11.0',
    'com.google.code.gson:gson:2.8.5',
    'org.apache.logging.log4j:log4j-api:2.11.1',
    'org.apache.logging.log4j:log4j-core:2.11.1',
    'com.squareup.okio:okio:1.14.0'
]
dependencies {
    deps.each{
        compile it
    }
}

task run(type: JavaExec, dependsOn: 'classes') {
    classpath = sourceSets.main.runtimeClasspath
    main = 'me.indexyz.strap.Main'
}

shadowJar {
    dependencies {
        deps.each{
            include dependency(it)
        }
    }
    classifier = 'full'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'me.indexyz.strap.Main'
        )
    }
}

build.dependsOn(shadowJar)